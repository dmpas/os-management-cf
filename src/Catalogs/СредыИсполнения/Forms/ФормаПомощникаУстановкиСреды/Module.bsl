// This Source Code Form is subject to the terms of the 
// Mozilla Public License, v.2.0. If a copy of the MPL 
// was not distributed with this file, You can obtain one 
// at http://mozilla.org/MPL/2.0/.


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияСуществующейУстановки(Команда)
	
	Элементы.Группа1.ТекущаяСтраница = Элементы.Группа1.ПодчиненныеЭлементы.ГруппаВводЦелевогоКаталога;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранКаталогИсполняемыхФайлов(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт

	Если (ВыбранныеФайлы = Неопределено)
		Или (ВыбранныеФайлы.Количество() = 0) Тогда
		
		Возврат;
		
	КонецЕсли;

	Объект.КаталогИсполняемыхФайлов = ВыбранныеФайлы.Получить(0);

КонецПроцедуры

&НаКлиенте
Процедура КаталогИсполняемыхФайловНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Не Элемент.КнопкаВыбора = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Показать(Новый ОписаниеОповещения("ВыбранКаталогИсполняемыхФайлов", ЭтаФорма));
	
КонецПроцедуры



&НаКлиенте
Процедура ВыполнитьНовуюУстановку(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Записать();
	ВыполнитьНовуюУстановкуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьНовуюУстановкуНаСервере()
	
	// TODO: Вынести установку в умный общий модуль
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	мКорневойКаталог = Новый Файл(ТекущийОбъект.КаталогИсполняемыхФайлов);
	Если не мКорневойКаталог.Существует() Тогда
		СоздатьКаталог(мКорневойКаталог.ПолноеИмя);
	КонецЕсли;
	
	ДанныеАрхива = ТекущийОбъект.ВерсияДвижка.ФайлАрхива.Получить();
	
	Распаковщик = Новый ЧтениеZipФайла(ДанныеАрхива.ОткрытьПотокДляЧтения());
	Распаковщик.ИзвлечьВсе(мКорневойКаталог.ПолноеИмя, РежимВосстановленияПутейФайловZIP.Восстанавливать);
	
	мКаталогИсполняемыхФайлов = Новый Файл(мКорневойКаталог.ПолноеИмя + "/bin");
	ТекущийОбъект.КаталогИсполняемыхФайлов = мКаталогИсполняемыхФайлов.ПолноеИмя;
	 
	Если Не ЗначениеЗаполнено(ТекущийОбъект.НаборБиблиотек) Тогда
		
		// При установке указали Новый набор
		НовыйНаборБиблиотек = Справочники.НаборыБиблиотек.СоздатьЭлемент();
		НовыйНаборБиблиотек.Наименование = СтрШаблон(НСтр("ru='Встроенный %1'"), мКорневойКаталог.ПолноеИмя);
		
		мКаталогБиблиотек = Новый Файл(мКорневойКаталог.ПолноеИмя + "/lib");
		
		НовыйНаборБиблиотек.КаталогБиблиотек = мКаталогБиблиотек.ПолноеИмя;
		НовыйНаборБиблиотек.Записать();
		
		ТекущийОбъект.НаборБиблиотек = НовыйНаборБиблиотек.Ссылка;
		
	КонецЕсли;
	
	ТекущийОбъект.Записать();
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	РаботаСДиалогами.РазрешитьВыборКаталогаВДиалоге(Элементы.КаталогИсполняемыхФайлов);
КонецПроцедуры
