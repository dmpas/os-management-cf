// This Source Code Form is subject to the terms of the 
// Mozilla Public License, v.2.0. If a copy of the MPL 
// was not distributed with this file, You can obtain one 
// at http://mozilla.org/MPL/2.0/.


&НаКлиенте
Процедура КодПриИзменении(Элемент)
	
	Элементы.Код.ОтметкаНезаполненного = Не ЗначениеЗаполнено(Объект.Код)
		Или ВерсияСоответствуетФормату(Объект.Код);
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ВерсияСоответствуетФормату(Знач КодВерсии)
	
	Попытка
		
		НенужнаяПеременная = СемантическоеВерсионирование.РазобратьВерсию(КодВерсии);
		
	Исключение
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПредставлениеРазмера(Знач РазмерВБайтах)
	
	МассивСуффиксов = СтрРазделить("Б,КиБ,МиБ,ГиБ", ",");
	ИндексСуффикса = 0;
	Размер = РазмерВБайтах;
	Пока (ИндексСуффикса < МассивСуффиксов.Количество()) И (Размер > 1000) Цикл // тут не опечатка
		Размер = Размер / 1024;
		ИндексСуффикса = ИндексСуффикса + 1;
	КонецЦикла;
	
	Возврат СтрШаблон("%1 %2", Формат(Размер, "ЧДЦ=2;"), МассивСуффиксов[ИндексСуффикса]);
	
КонецФункции

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	мДанныеАрхива = ТекущийОбъект.ФайлАрхива.Получить();
	ЕстьАрхив = Не мДанныеАрхива = Неопределено;
	Если ЕстьАрхив Тогда
		
		СведенияОбАрхиве = СтрШаблон(НСтр("ru='Загружен архив, размер %1';"),
				ПолучитьПредставлениеРазмера(мДанныеАрхива.Размер()));
				
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПустаяСтрока(СведенияОбАрхиве) Тогда
		СведенияОбАрхиве = НСтр("ru='Архив не загружен. Загрузить.';");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьССайта(Команда)
	СкачатьАрхивССайта();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьАрхив(Команда)
	
	АдресПомещения = ?(ПустаяСтрока(АдресХраненияАрхива), Неопределено, АдресХраненияАрхива);
	НачатьПомещениеФайла(Новый ОписаниеОповещения("ПриОкончанииЗагрузкиФайлаСДиска", ЭтаФорма), АдресПомещения,,
		Истина, ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры



&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ИзмененФайлАрхива = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура СкачатьАрхивССайта()
	
	ВерсияОбъект = СемантическоеВерсионирование.РазобратьВерсию(Объект.Код);
	АдресРесурса = СтрШаблон("/downloads/%1_%2_%3/OneScript-%1.%2.%3.zip",
		ВерсияОбъект.Мажор,
		ВерсияОбъект.Минор,
		ВерсияОбъект.Патч)
	;
	
	АдресПомещения = ?(ПустаяСтрока(АдресХраненияАрхива), ЭтаФорма.УникальныйИдентификатор, АдресХраненияАрхива);
	
	Попытка
		
		АдресХраненияАрхива = Справочники.ВерсииДвижка.ПолучитьФайлСОфициальногоСайта(АдресРесурса, АдресПомещения);
		
	Исключение
		
		СообщениеОНеУспешномСкачивании = Новый СообщениеПользователю();
		СообщениеОНеУспешномСкачивании.Поле = "Код";
		СообщениеОНеУспешномСкачивании.ПутьКДанным = "Код";
		СообщениеОНеУспешномСкачивании.Текст = ОписаниеОшибки();
		СообщениеОНеУспешномСкачивании.Сообщить();
		
		Возврат;
		
	КонецПопытки;
	
	ИзмененФайлАрхива = Истина;
	Модифицированность = Истина;
	Объект.ПрямаяСсылкаНаАрхив = "http://oscript.io" + АдресРесурса;
	ОбновитьСведенияОбАрхиве();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСведенияОбАрхиве()
	
	мДанныеАрхива = ПолучитьИзВременногоХранилища(АдресХраненияАрхива);
	СведенияОбАрхиве = СтрШаблон(НСтр("ru='Загружен архив, размер %1';"),
			ПолучитьПредставлениеРазмера(мДанныеАрхива.Размер()));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОкончанииЗагрузкиФайлаСДиска(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	АдресХраненияАрхива = Адрес;
	ИзмененФайлАрхива = Истина;
	Модифицированность = Истина;
	
	ОбновитьСведенияОбАрхиве();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ИзмененФайлАрхива Тогда
		
		ТекущийОбъект.ФайлАрхива = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресХраненияАрхива));
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресХраненияАрхива()
	
	Если ЗначениеЗаполнено(АдресХраненияАрхива) Тогда
		Возврат АдресХраненияАрхива;
	КонецЕсли;
	
	ЭлементОбъект = РеквизитФормыВЗначение("Объект");
	Возврат ПоместитьВоВременноеХранилище(ЭлементОбъект.ФайлАрхива.Получить());
	
КонецФункции

&НаКлиенте
Процедура ОкончаниеВыгрузкиАрхива(ПолученныеФайлы, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьФайлАрхива(Команда)
	
	ИмяФайла = "OneScript-" + Объект.Код + ".zip";
	АдресХраненияАрхива = ПолучитьАдресХраненияАрхива();
	ОписаниеФайлаАрхива = Новый ОписаниеПередаваемогоФайла(ИмяФайла, АдресХраненияАрхива);
	ПолучаемыеФайлы = Новый Массив;
	ПолучаемыеФайлы.Добавить(ОписаниеФайлаАрхива);
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Фильтр = НСтр("ru='ZIP-архивы|*.zip';");
	
	НачатьПолучениеФайлов(Новый ОписаниеОповещения("ОкончаниеВыгрузкиАрхива", ЭтаФорма), ПолучаемыеФайлы, Диалог, Истина);
	
КонецПроцедуры
