// This Source Code Form is subject to the terms of the 
// Mozilla Public License, v.2.0. If a copy of the MPL 
// was not distributed with this file, You can obtain one 
// at http://mozilla.org/MPL/2.0/.


&НаКлиенте
Процедура КодПриИзменении(Элемент)
	
	Элементы.Код.ОтметкаНезаполненного = Не ЗначениеЗаполнено(Объект.Код)
		Или ВерсияСоответствуетФормату(Объект.Код);
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ВерсияСоответствуетФормату(Знач КодВерсии)
	
	Попытка
		
		НенужнаяПеременная = СемантическоеВерсионирование.РазобратьВерсию(КодВерсии);
		
	Исключение
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПредставлениеРазмера(Знач РазмерВБайтах)
	
	МассивСуффиксов = СтрРазделить("Б,КиБ,МиБ,ГиБ", ",");
	ИндексСуффикса = 0;
	Размер = РазмерВБайтах;
	Пока (ИндексСуффикса < МассивСуффиксов.Количество()) И (Размер > 1000) Цикл // тут не опечатка
		Размер = Размер / 1024;
		ИндексСуффикса = ИндексСуффикса + 1;
	КонецЦикла;
	
	Возврат СтрШаблон("%1 %2", Формат(Размер, "ЧДЦ=2;"), МассивСуффиксов[ИндексСуффикса]);
	
КонецФункции

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	мДанныеАрхива = ТекущийОбъект.ФайлАрхива.Получить();
	ЕстьАрхив = Не мДанныеАрхива = Неопределено;
	Если ЕстьАрхив Тогда
		
		СведенияОбАрхиве = СтрШаблон(НСтр("ru='Загружен архив, размер %1';"),
				ПолучитьПредставлениеРазмера(мДанныеАрхива.Размер()));
				
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СведенияОбАрхиве = НСтр("ru='Архив не загружен. Загрузить.';");
	
КонецПроцедуры



&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ИзмененФайлАрхива = Ложь;
	
КонецПроцедуры


&НаСервере
Процедура СкачатьАрхивССайта()
	
	АдресПомещения = ?(ПустаяСтрока(АдресХраненияАрхива), ЭтаФорма.УникальныйИдентификатор, АдресХраненияАрхива);
	АдресХраненияАрхива = Справочники.ВерсииДвижка.ПолучитьВерсиюСОфициальногоСайта(Объект.Код, АдресПомещения);
	ИзмененФайлАрхива = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриВыбореАрхива(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКАрхиву = ВыбранныеФайлы.Получить(0);
	АдресПомещения = ?(ПустаяСтрока(АдресХраненияАрхива), Неопределено, АдресХраненияАрхива);
	
	// TODO: Веб-клиент
	НачатьПомещениеФайла(Новый ОписаниеОповещения("ПриОкончанииЗагрузкиФайлаСДиска", ЭтаФорма),
		АдресПомещения, ПутьКАрхиву, , ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОкончанииЗагрузкиФайлаСДиска(Результат, Адрес, ВыбранноеИмяФайла) Экспорт
	
	СведенияОбАрхиве = НСтр("ru='Файл успешно загружен!';");
	ИзмененФайлАрхива = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ИзмененФайлАрхива Тогда
		
		ТекущийОбъект.ФайлАрхива = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресХраненияАрхива));
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура СведенияОбАрхивеНажатие(Элемент, СтандартнаяОбработка)
	
	// TODO: Красивая выбиралка
	
	СписокВариантовВыбора = Новый СписокЗначений();
	СписокВариантовВыбора.Добавить("ССайта", НСтр("ru='Скачать с OScript.io';"));
	СписокВариантовВыбора.Добавить("ИзФайла", НСтр("ru='Загрузить zip-архив';"));
	
	ВыбранныйСпособ = ВыбратьИзСписка(СписокВариантовВыбора, Элемент, СписокВариантовВыбора.Получить(0));
	Если ВыбранныйСпособ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйСпособ.Значение = "ССайта" Тогда
		СкачатьАрхивССайта();
	Иначе
		
		ДиалогВыбораАрхива = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ДиалогВыбораАрхива.Фильтр = "*.zip";
		ДиалогВыбораАрхива.Показать(Новый ОписаниеОповещения("ПриВыбореАрхива", ЭтаФорма));
		 
	КонецЕсли;
	
КонецПроцедуры

